resource "aws_instance" "catalogue" {
    ami = local.ami_id
    instance_type = var.instance_type
    subnet_id = local.private_subnet_id
    vpc_security_group_ids = [local.catalogue_sg_id]

    tags = merge(
        {
            Name = "${var.project}-${var.environment}-catalogue"
        },

        local.common_tags
    )
  
}

resource "terraform_data" "catalogue" {
  triggers_replace = [
    aws_instance.catalogue.id
  ]

  connection {
    type     = "ssh"
    user     = "ec2-user"
    password = "DevOps321"
    host     = aws_instance.catalogue.private_ip
  }

  # ✅ Step 1: Copy file
  provisioner "file" {
    source      = "bootstrap.sh"
    destination = "/tmp/bootstrap.sh"
  }

  # ✅ Step 2: Execute file
  provisioner "remote-exec" {
    inline = [
      "chmod +x /tmp/bootstrap.sh",
      "sudo sh /tmp/bootstrap.sh catalogue ${var.environment} ${var.varsion}"
    ]
  }
}

 resource "aws_ec2_instance_state" "catalogue" {
  instance_id = aws_instance.catalogue.id
  state = "stopped"
  depends_on = [ terraform_data.catalogue ]
  
}

resource "aws_ami_from_instance" "catalogue_ami" {
  # roboshop-dev-catalogue-v3-instandid will create name
  name               = "${var.project}-${var.environment}-catalogue-${var.varsion}-${aws_instance.catalogue.id}"
  source_instance_id = aws_instance.catalogue.id

  depends_on = [aws_ec2_instance_state.catalogue]
  

  tags = merge(
    {
      Name = "${var.project}-${var.environment}-catalogue-ami"
    },
    local.common_tags
  )
}

resource "aws_lb_target_group" "catalogue" {
  name        = "${var.project}-${var.environment}-catalogue" 
  port        = 8080
  protocol    = "HTTP"
  vpc_id      = local.vpc_id
  deregistration_delay = 60

  health_check {
    healthy_threshold = 2
    interval = 10
    matcher = "200-299"
    path = "/health"
    port = 8080
    protocol = "HTTP"
    timeout = 5
    unhealthy_threshold = 3
  }

}

resource "aws_launch_template" "catalogue" {
  name = "${var.project}-${var.environment}-catalogue" 

  image_id = aws_ami_from_instance.catalogue_ami.id

  # once autosaclling is less traffic it will terminate instance
  instance_initiated_shutdown_behavior = "terminate"

  instance_type = var.instance_type
  vpc_security_group_ids = [local.catalogue_sg_id]

  # each time we apply terraform this version will be updated as default
  update_default_version = true

# tags for instance created by launch_template through autoscalling 
tag_specifications {
    resource_type = "instance"

    tags = merge(
        {
          Name = "${var.project}-${var.environment}-catalogue"
        },
        local.common_tags
      )
  }
  # tags for volume created by instance 
tag_specifications {
    resource_type = "volume"

    tags = merge(
        {
          Name = "${var.project}-${var.environment}-catalogue"
        },
        local.common_tags
      )
}
# tags for launch template
  tags = merge(
        {
          Name = "${var.project}-${var.environment}-catalogue"
        },
        local.common_tags
      )
}

resource "aws_autoscaling_group" "catalogue" {
  name                      = "${var.project}-${var.environment}-catalogue"
  max_size                  = 10
  min_size                  = 2
  health_check_grace_period = 120
  health_check_type         = "ELB"
  desired_capacity          = 2
  force_delete              = false

  launch_template {
    id = aws_launch_template.catalogue.id
    version = "$Latest"
  }

  vpc_zone_identifier       = [local.private_subnet_id]
  target_group_arns = [aws_lb_target_group.catalogue.arn]

  instance_refresh {
    strategy = "Rolling"
    preferences {
      min_healthy_percentage = 50
    }
    triggers = ["launch_template"]
  }

dynamic "tag" {
  for_each = {
    name = "${var.project}-${var.environment}-catalogue"
  }

  content {
    key                 = tag.key
    value               = tag.value
    propagate_at_launch = true
  }
}
} 

resource "aws_autoscaling_policy" "catalogue" {
  name                   = "${var.project}-${var.environment}-catalogue"
  autoscaling_group_name = aws_autoscaling_group.catalogue.name

  policy_type = "TargetTrackingScaling"

  target_tracking_configuration {
    predefined_metric_specification {
      predefined_metric_type = "ASGAverageCPUUtilization"
    }

    target_value = 70.0
  }
}

resource "aws_lb_listener_rule" "catalogue" {
  listener_arn = local.backend_alb_listener_arn
  priority     = 10

  action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.catalogue.arn
  }

  condition {
    host_header {
      values = ["catalogue.backend-alb-${var.environment}.${var.domain_name}"]
    }
  }
}

resource "terraform_data" "catalogue_delete" {
  triggers_replace = [
    aws_instance.catalogue.id
  ]
  depends_on = [ aws_lb_listener_rule.catalogue ]

 #its exicute in bastion 
  provisioner "local-exec" {
    command = "aws ec2 terminate-instances --instance-ids ${aws_instance.catalogue.id}"
    
  }
  
}